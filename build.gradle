import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'java'
apply plugin: 'application'
apply from: 'template.properties'
/*
//template.properties example:
ext {
    clientId = '0000000'
    clientSecret = '00000000000000000000'
    clientEmail = 'example@hell.com'
    clientPass = 'password123'
}
*/

mainClassName = "com.vk.api.examples.oauth.user.Application"

ext {
    log4jVersion = '2.7'
    jsoupVersion = '1.7.2'
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4jVersion
    compile group: 'org.jsoup', name: 'jsoup', version: jsoupVersion
    compile 'com.vk.api:sdk:0.3.5' 
}

jar {
    manifest {
        attributes(
            'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
            'Main-Class': mainClassName
        )
    }
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Gradle Jar File Example',
            'Implementation-Version': '1.0',
            'Main-Class': mainClassName
    }
    baseName = "vkstatusdrifter"
    //buildDir = new File("~/gradleBuild/" + project.name)
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task service(type: Copy) {
    from fatJar
    into "$home/bin"
} 

processResources {
    eachFile { details ->
        if (details.name.endsWith('.properties')) {
            details.filter(ReplaceTokens, tokens: [
                    'client.email': clientEmail,
                    'client.pass': clientPass,
                    'client.id': clientId,
                    'client.secret': clientSecret,
            ])
        }
    }
}
